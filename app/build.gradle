apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.freefair.android-ci'

def tempVersion = 'git describe --abbrev=0 --tags'.execute([],rootProject.projectDir).text.trim()

if(hasProperty('version') && !'unspecified'.equals(version)) {
    tempVersion = version
}

android {
    signingConfigs {
        releaseSigningConfig {
            keyAlias bpm_meter_android_key_alias
            keyPassword bpm_meter_android_key_password
            storeFile file('../release.keystore')
            storePassword bpm_meter_android_keystore_password
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "com.larsgrefer.bpmMeter"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 14
        versionName tempVersion
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSigningConfig
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

play {
    jsonFile = new File('playstore-apikey.json');
    uploadImages = true
    track = 'beta'
}

dependencies {
    compile 'com.android.support:design:25.3.1'
    compile 'io.freefair.android-colors:material:1.2.0'
    compile 'io.freefair.android-library:appcompat-preference:1.0.0-rc1'
    compile 'io.freefair.android-injection:injection:2.0.0-beta1'

    provided "org.projectlombok:lombok:1.16.16"
}
